group 'com.mitchellbosecke'
version '1.0-SNAPSHOT'

// dependencies for the build script itself (i.e. not the application)
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    }
}


apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

//sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()

    // Repositories for the discord dependency
    jcenter()
    maven {
        url  "https://jitpack.io"
    }
}

sourceSets {
    integrationTest{
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
        java.srcDir file ('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jar {
    manifest {
        attributes("Implementation-Title": "SeniorCommander",
                "Implementation-Version": version,
                "Main-Class": "com.mitchellbosecke.seniorcommander.SeniorCommander")
    }
}

dependencies {

    // configuration
    compile group: 'com.typesafe', name: 'config', version: '1.3.0'

    // database
    compile group: 'com.mitchellbosecke', name: 'seniorcommander-jpa', version: '1.0-SNAPSHOT'
    compile group: 'org.postgresql', name: 'postgresql', version: '9.4.1208'
    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.2.3.Final'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '4.0.3'

    // json
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.3'

    // irc
    compile group: 'org.pircbotx', name: 'pircbotx', version: '2.1'

    // discord
    compile group: 'com.github.austinv11', name: 'Discord4j', version: '2.7.0'

    // twitch
    compile group: 'com.mitchellbosecke', name: 'twitchapi', version: '1.0-SNAPSHOT'

    // AI
    compile group: 'ai.api', name: 'libai', version: '1.3.5'

    // logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.21'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.21'
    //compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.21'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'

    // testing
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

